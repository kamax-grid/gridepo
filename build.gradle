/*
 * Gridepo - Grid Data Server
 * Copyright (C) 2019 Kamax Sarl
 *
 * https://www.kamax.io/
 *
 * This program is free software: you can redistribute it and/or modify
 * it under the terms of the GNU Affero General Public License as
 * published by the Free Software Foundation, either version 3 of the
 * License, or (at your option) any later version.
 *
 * This program is distributed in the hope that it will be useful,
 * but WITHOUT ANY WARRANTY; without even the implied warranty of
 * MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 * GNU Affero General Public License for more details.
 *
 * You should have received a copy of the GNU Affero General Public License
 * along with this program.  If not, see <http://www.gnu.org/licenses/>.
 */

import java.util.regex.Pattern

String appVersion() {
    String v = System.getenv('GRIDEPO_BUILD_VERSION')

    if (v == null || v.size() == 0) {
        ByteArrayOutputStream out = new ByteArrayOutputStream()

        def o = exec {
            commandLine = ['git', 'describe', '--tags', '--always', '--dirty']
            standardOutput = out
            errorOutput = out
            ignoreExitValue = true
        }

        if (o.exitValue != 0) {
            if (o.exitValue != 128) {
                printf("Unable to determine git version: %s", out.toString())
            }

            return "UNKNOWN"
        }

        v = out.toString().replace(System.lineSeparator(), '')
    }

    return Pattern.compile("v(\\d+\\.)?(\\d+\\.)?(\\d+)(-.*)?").matcher(v).matches() ? v.substring(1) : v
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'idea'

group = 'io.kamax.grid'
version = appVersion()
mainClassName = 'io.kamax.grid.gridepo.MonolithHttpGridepoApplication'

repositories {
    jcenter()
}

dependencies {
    // Logging
    compile 'org.slf4j:slf4j-simple:1.7.25'

    // Various utilities
    compile 'org.apache.commons:commons-lang3:3.5'
    compile 'commons-io:commons-io:2.6'
    compile 'commons-codec:commons-codec:1.11'
    compile 'com.google.guava:guava:26.0-jre'

    // JSON handling
    compile 'com.google.code.gson:gson:2.8.5'

    // Config management
    compile 'org.yaml:snakeyaml:1.23'

    // Connection Pool
    compile 'com.mchange:c3p0:0.9.5.2'

    // PostgreSQL
    compile 'org.postgresql:postgresql:42.2.2'

    // Crypto
    compile 'net.i2p.crypto:eddsa:0.3.0'

    // Event bus
    compile 'net.engio:mbassador:1.3.1'

    // DNS lookups
    compile 'dnsjava:dnsjava:2.1.8'

    // HTTP Client
    compile 'org.apache.httpcomponents:httpclient:4.5.4'

    // JWT for access tokens
    compile 'com.auth0:java-jwt:3.3.0'

    // HTTP server
    compile 'io.undertow:undertow-core:2.0.16.Final'

    // TLS
    compile 'org.bouncycastle:bcprov-jdk16:1.46'

    testCompile 'junit:junit:4.12'

}
